(ns kaaf.dsl.aggregator
  (:require kaaf.contracts.core
            [mint.validation.mappings :as mvm]))


(defn matching-event?
  "Finds relevant events generated by each action.
  x-hs-request-id field is common key in the action
  payload and the event payload."
  [{{action-id "x-hs-request-id"} :headers
    :as action-ctx-map}
   {{event-id "request_id"} :value
    :as event-map}]
  (= action-id event-id))


(defn get-event-type
  "Fetches event id from the event payload."
  [event-map]
  (get-in event-map [:value "type"]))


(defn action+events
  "Punches event (full payload) into the respective action payload."
  [events action]
  (let [events-matched
        (->> events
             (filterv (partial matching-event? action)))]
    (assoc action
           :events events-matched)))


(defn action+events->summarize
  "Punches event (only event type) into the respective action payload."
  [events action]
  (let [events-matched
        (->> events
             (filterv (partial matching-event? action))
             (mapv (fn [event-map]
                     (let [event-type-val (get (:value event-map)
                                               "type")]
                       (mvm/codes->types event-type-val)))))]
    (assoc action :events events-matched)))
